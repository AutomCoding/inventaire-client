#!/usr/bin/env coffee
CONFIG = require 'config'
__ = CONFIG.universalPath
{ min } = require 'lodash'
Promise = require 'bluebird'
fs = require 'graceful-fs'
# readFile = Promise.promisify fs.readFile
writeFile = Promise.promisify fs.writeFile

getLanguageStats = require './lib/i18n/get_language_stats'
langs = require('./lib/i18n/langs').active

languagesDataPath = __.path 'client', 'app/lib/languages_data.coffee'

# Regions that aren't "#{lang}_#{lang.toUpperCase()}"
specialCaseRegions =
  da: 'da_DK'
  en: 'en_US'
  ja: 'ja_JP'
  no: 'nb_NO'
  sv: 'sv_SE'

updateData = (languagesData)->
  json = JSON.stringify languagesData, null, 2
  text = """# Generated by scripts/update_languages_stats
  module.exports = """ + json
  writeFile languagesDataPath, text

completionData = {}
for lang in langs
  completionData[lang] = getLanguageStats lang

Promise.props completionData
.then (results)->
  languagesData = {}
  engKeysNum = results.en
  for lang, keysNum of results
    languagesData[lang] =
      defaultRegions: specialCaseRegions[lang] or "#{lang}_#{lang.toUpperCase()}"

    completion = Math.round keysNum/engKeysNum*100
    # When keys are deleted in the English version but not yet uploaded,
    # some language may appear with more than 100% completion
    languagesData[lang].completion = min [ completion, 100 ]
    console.log "#{lang} completion", languagesData[lang]

  updateData languagesData
