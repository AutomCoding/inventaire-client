#!/usr/bin/env coffee
CONFIG = require 'config'
__ = CONFIG.universalPath
_ = __.require 'builders', 'utils'
{ Promise } = __.require 'lib', 'promises'
fs = require 'graceful-fs'
readFile = Promise.promisify fs.readFile
writeFile = Promise.promisify fs.writeFile

getLanguageStats = require './lib/i18n/get_language_stats'
langs = require('./lib/i18n/langs').active

languagesDataPath = __.path 'client', 'app/lib/languages_data.coffee'
languagesData = require languagesDataPath

updateData = ->
  json = JSON.stringify languagesData, null, 2
  readFile languagesDataPath, {encoding: 'utf-8'}
  .then (text)->
    text = text.split('{')[0] + json
    writeFile languagesDataPath, text


completionData = {}
for lang in langs
  completionData[lang] = getLanguageStats lang


Promise.props completionData
.then (results)->
  engKeysNum = results.en
  for lang, keysNum of results
    languagesData[lang] or= {}
    languagesData[lang].completion = Math.round keysNum/engKeysNum*100
    console.log "#{lang} completion", languagesData[lang]

  updateData()
